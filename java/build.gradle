plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

wrapper {
    gradleVersion = '4.8.1'
    distributionType = Wrapper.DistributionType.ALL
}

group = 'com.davipb'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

lombok {
    version = '1.18.0'
    sha256 = ''
}

import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask) {
    dependsOn compileJava

    def outputDir = file("$buildDir/delombok")
    outputs.dir outputDir
    sourceSets.main.java.srcDirs.each {
        inputs.dir it
        args it, "-d", outputDir
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'JamSpell Java', 'Implementation-Version': version
        attributes 'Specification-Title': 'JamSpell C++', 'Specification-Version': '0.0.11'
    }
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

def description = 'A Java binding for JamSpell, a modern spellchecking library'
def repository = 'https://github.com/Davipb/jamspell-java.git'
def url = 'https://github.com/Davipb/jamspell-java'
def issuesUrl = 'https://github.com/Davipb/jamspell-java/issues'

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'JamSpell Java Binding')
                root.appendNode('description', description)
                root.appendNode('url', url)
                root.appendNode('inceptionYear', '2018')

                def licenses = root.appendNode('licenses')
                def mit = licenses.appendNode('license')
                mit.appendNode('name', 'MIT License')
                mit.appendNode('url', 'https://raw.githubusercontent.com/Davipb/jamspell-java/master/LICENSE')
                mit.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def davipb = developers.appendNode('developer')
                davipb.appendNode('id', 'Davipb')
                davipb.appendNode('name', 'Davipb')
                davipb.appendNode('email', 'daviparca@gmail.com')

                def issueManagement = root.appendNode('issueManagement')
                issueManagement.appendNode('system', 'GitHub')
                issueManagement.appendNode('url', issuesUrl)

                def scm = root.appendNode('scm')
                scm.appendNode('connection', "scm:git:$repository")
                scm.appendNode('developerConnection', "scm:git:$repository")
                scm.appendNode('tag', 'HEAD')
                scm.appendNode('url', url)
            }
        }
    }
}

bintray {
    user = project.ext.'bintray.user'
    key = project.ext.'bintray.key'
    pkg {
        repo = 'maven'
        name = project.name
        desc = description
        websiteUrl = url
        issueTrackerUrl = issuesUrl
        vcsUrl = url
        licenses = [ 'MIT' ]
        labels = [ 'spellcheck', 'spellchecker', 'java', 'jni', 'jamspell' ]
        publicDownloadNumbers = true

        githubRepo = repository

        version {
            name = project.version
            released = new Date()

            gpg {
                sign = true
                passphrase = project.ext.'bintray.pkg.version.gpg.passphrase'
            }
        }
    }
    publications = [ 'maven' ]
}
