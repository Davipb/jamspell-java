plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

wrapper {
    gradleVersion = '4.8.1'
    distributionType = Wrapper.DistributionType.ALL
}

group = 'com.davipb'
version = '0.1.0'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:16.0.2'
    implementation 'org.apache.commons:commons-lang3:3.7'
}

lombok {
    version = '1.18.0'
    sha256 = ''
}

import io.franzbecker.gradle.lombok.task.DelombokTask
task delombok(type: DelombokTask) {
    dependsOn compileJava
    classpath configurations.compileClasspath

    ext.outputDir = file("$buildDir/delombok")
    outputs.dir outputDir
    sourceSets.main.java.srcDirs.each {
        inputs.dir it
        args it, "-d", outputDir
    }
}

apply from: 'package.gradle'
apply from: 'publish.gradle'
